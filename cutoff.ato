from "generics/interfaces.ato" import Power
import Resistor
from "tip120.ato" import TIP120
from "screwterms.ato" import Screwterms2P
from "lithiumsc.ato" import LIC0825Q3R8506
from "1sma5915bt3g.ato" import _1SMA5915BT3G

"""
Spacebridge HAB Cutoff Mechanism
Notes:
    Lithium supercap cannot exceed 3.7V, sense SC voltage with ESP32-S3 ADC to cutoff charging at the limit. High series resistance + zener diode is sufficient protection, but wastes energy
"""

module Cutoff:
    power = new Power
    bjt_cutoff = new TIP120
    bjt_SC = new TIP120
    screwterms = new Screwterms2P
    supercap = new LIC0825Q3R8506
    Rsip = new Resistor; Rsip.value = 1kohm +/- 20%; Rsip.package = "R0402"
    zener = new _1SMA5915BT3G #3.9V nom zener diode OVP
  
    signal SC_GATE ~ bjt_SC.B # Dis/connect supercapacitor from charging
    signal CUTOFF_GATE ~ bjt_cutoff.B # Fire cutoff
    signal SC_Sense ~ supercap.p1 # Sense capacitor voltage
    power.vcc ~ Rsip.p1; Rsip.p2 ~ supercap.p1; supercap.p2 ~ bjt_SC.C; bjt_SC.E ~ power.gnd
    zener.K ~ supercap.p1; zener.A ~ power.gnd # Reverse conducts through SC/Rsip when SC_V >= 3.9V, redundancy in case uC fails to disconnect SC
    # Nichrome attaches across screw terminals
    screwterms.p1 ~ supercap.p1; screwterms.p2 ~ bjt_cutoff.C; bjt_cutoff.E ~ power.gnd

